/*
 * Copyright (c) 2017 Jean Guyomarc'h
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the "Software"),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
 * OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

/*
 * NOTE: this file and associated graphics are imported/recycled from Terminology
 * See https://www.enlightenment.org/about-terminology for details.
 */

group { "eovim/tab";
   images {
      image: "tab_base_l1.png" COMP;
      image: "tab_base_m.png" COMP;
      image: "tab_base_r1.png" COMP;
      image: "tab_other_l.png" COMP;
      image: "tab_other_m.png" COMP;
      image: "tab_other_r.png" COMP;
      image: "tab_shad_l2.png" COMP;
      image: "tab_shad_r2.png" COMP;
      image: "tab_close.png" COMP;
   }
   parts {
      image { "tab_bevel_shad1"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel1.to: "tab_bevel_top1";
            rel2.to: "tab_bevel_top1";
            image.normal: "tab_shad_l2.png";
            image.border: 0 0 10 10;
            fill.smooth: 0;
         }
      }
      image { "tab_bevel_shad3"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel1.to: "tab_bevel_top3";
            rel2.to: "tab_bevel_top3";
            image.normal: "tab_shad_r2.png";
            image.border: 0 0 10 10;
            fill.smooth: 0;
         }
      }

      /*=====================================================================
       * Background tabs.
       *
       * The active tab does not show these parts, the other tabs do.
       *=====================================================================*/
      image { "tab_base_top1"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel.to: "tab_bevel_top1";
            image.normal: "tab_other_l.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
         }
         desc { "active";
            inherit: "default";
            visible: 0;
         }
      }
      image { "tab_base_top2"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel.to: "tab_bevel_top2";
            image.normal: "tab_other_m.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
         }
         desc { "active";
            inherit: "default";
            visible: 0;
         }
      }
      image { "tab_base_top3"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel.to: "tab_bevel_top3";
            image.normal: "tab_other_r.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
         }
         desc { "active";
            inherit: "default";
            visible: 0;
         }
      }
      rect { "inactive_sep"; nomouse;
         desc { "default";
            color: 200 200 200 64;
            rel1.to_x: "tab_base_top1";
            rel2.to_x: "tab_base_top3";
            rel1.relative: 0.0 1.0;
            min: 1 1;
            align: 0.5 1.0;
         }
         desc { "active";
            inherit: "default";
            visible: 0;
         }
      }



      image { "tab_bevel_top1"; nomouse;
         clip_to: "clip";
         desc { "default";
            align: 0.0 0.0;
            rel1.offset: -4 0;
            max: 12 99999;
            min: 12 0;
            image.normal: "tab_base_l1.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
            fixed: 1 0;
         }
      }
      image { "tab_bevel_top2"; nomouse;
         clip_to: "clip";
         desc { "default";
            rel1.to: "tab_bevel_top1";
            rel1.relative: 1.0 0.0;
            rel2.to: "tab_bevel_top3";
            rel2.relative: 0.0 1.0;
            image.normal: "tab_base_m.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
         }
      }
      image{ "tab_bevel_top3"; nomouse;
         clip_to: "clip";
         desc { "default";
            align: 1.0 0.0;
            rel2.offset: 3 -1;
            max: 12 99999;
            min: 12 0;
            image.normal: "tab_base_r1.png";
            image.border: 0 0 4 4;
            fill.smooth: 0;
            fixed: 1 0;
         }
      }
      rect { "clip"; nomouse;
         desc { "default";
            rel2.offset: -1 -2;
         }
      }
      rect { "event"; /* Event catcher */
         desc { "default";
            color: 0 0 0 0;
         }
      }
      text { "eovim.tab.title"; nomouse;
         clip_to: "clip";
         effect: SOFT_SHADOW BOTTOM;
         desc { "default";
            color: 188 188 188 255;
            color3: 0 0 0 18;
            text {
               font: "Sans";
               size: 12;
               ellipsis: 1.0;
               min: 1 1;
            }
         }
         desc { "active";
            inherit: "default";
            color: 225 225 225 255;
            text {
               font: "Sans:style=Bold";
            }
         }
      }
      image { "tabclose";
         clip_to: "clip";
         desc { "default";
            image.normal: "tab_close.png";
            rel1.relative: 1.0 0.0;
            rel1.offset: -40 0;
            rel2.offset: -4 0;
            min: 18 18;
            max: 18 18;
            align: 1.0 0.5;
            visible: 0;
         }
         desc { "active";
            inherit: "default";
            visible: 1;
         }
      }
   }
   programs {
      program { signal: "eovim,tab,activate"; source: "eovim";
         action: STATE_SET "active";
         target: "eovim.tab.title";
         target: "tab_base_top1";
         target: "tab_base_top2";
         target: "tab_base_top3";
         target: "inactive_sep";
         target: "tabclose";
      }
      program {
         signal: "mouse,clicked,1"; source: "tabclose";
         action: SIGNAL_EMIT "tab,close" "eovim";
      }
      program {
         signal: "mouse,clicked,1"; source: "event";
         action: SIGNAL_EMIT "tab,activate" "eovim";
      }
   }
}
